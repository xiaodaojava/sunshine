plugins {
	id 'java'
	id 'maven-publish'
	id 'java-library'
//	id 'signing'
}


dependencies {
	api project(":sunshine-base")
	compileOnly group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'
	compileOnly group: 'ognl', name: 'ognl', version: '3.2.10'
	compileOnly group: 'org.apache.poi', name: 'poi', version: '4.1.2'
	compileOnly group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
	compileOnly group: 'org.yaml', name: 'snakeyaml', version: '1.32'
	compileOnly group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
	compileOnly (group: 'com.ctrip.framework.apollo', name: 'apollo-client', version: '1.3.0')
	compileOnly group: 'org.mybatis', name: 'mybatis', version: '3.5.6'
	compileOnly group: 'io.lettuce', name: 'lettuce-core', version: '6.0.1.RELEASE'
	compileOnly group: 'com.hierynomus', name: 'sshj', version: '0.27.0'
	compileOnly group: 'com.vladsch.flexmark',name: 'flexmark', version: '0.61.24'
	compileOnly group: 'com.vladsch.flexmark', name: 'flexmark-ext-gfm-strikethrough', version: '0.61.24'
	compileOnly group: 'com.vladsch.flexmark', name: 'flexmark-ext-tables', version: '0.61.24'
	compileOnly group: 'com.aliyun.oss', name: 'aliyun-sdk-oss', version: '3.11.0'
	compileOnly group: 'com.alibaba.nacos', name: 'nacos-client', version: '1.4.4'
	compileOnly group: 'org.apache.dubbo', name: 'dubbo', version: '2.7.15'

//	implementation group: 'io.netty', name: 'netty-all', version: '4.1.50.Final'


}

// 后面的都是打包的配置
task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}
// 生成 javadoc jar
task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}
// javadoc 配置，这里是自定义了 java doc 的一些配置
javadoc {
	description = "Generates project-level javadoc for use in -javadoc jar"

	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.version = true
	options.header = project.name
	options.addStringOption('Xdoclint:none', '-quiet')

	// suppress warnings due to cross-module @see and @link references;
	// note that global 'api' task does display all warnings.
	logging.captureStandardError LogLevel.INFO
	logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
	options.encoding = "UTF-8"  //编码一定要配置否则直接出错
	options.charSet = 'UTF-8'
}

publishing {
	repositories {

		maven {
			name 'sonatypeRepository'
			url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			credentials {
				username = project.findProperty("sonatype.username")
				password = project.findProperty("sonatype.password")
			}
		}


		publications {
			mavenJava(MavenPublication) {
				groupId = group
				version = version
				from components.java
				artifact sourcesJar
				artifact javadocJar
				pom {
					name = "sunshine-common"
					description = "java tools depends on third party"
					url = "https://github.com/xiaodaojava/sunshine"
					licenses {
						license {
							name = "The Apache License, Version 2.0"
							url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
						}
					}
					developers {
						developer {
							id = "lixiang"
							name = "lixiang"
							email = "lixiang9409@vip.qq.com"
						}
					}
					scm {
						connection = "scm:git:https://github.com/xiaodaojava/sunshine.git"
						developerConnection = "scm:git:https://github.com/xiaodaojava/sunshine.git"
						url = "https://github.com/xiaodaojava/sunshine"
					}
				}

			}
		}
	}
}

//signing {
//	sign publishing.publications.mavenJava
//}


